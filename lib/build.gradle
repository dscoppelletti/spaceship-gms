
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
//    id 'org.jetbrains.dokka'
    id 'it.scoppelletti.spaceship.android-library'
}

group 'it.scoppelletti.spaceship'
version '1.0.0'
description 'Spaceship for Google Play Services'
archivesBaseName = 'spaceship-gms'

android {
    compileSdkVersion sdk_target
    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-gms'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-gms.git'
    inceptionYear = '2019-2021'
}

configurations {
    bom
    api.extendsFrom(bom)
    implementation.extendsFrom(bom)
    kapt.extendsFrom(bom)
    coreLibraryDesugaring.extendsFrom(bom)
}

dependencies {
    bom platform(BOM)
    bom platform("com.google.firebase:firebase-bom:${firebase_version}")
    api 'androidx.annotation:annotation'
    api 'androidx.appcompat:appcompat'
    api 'androidx.constraintlayout:constraintlayout'
    api 'com.google.android.gms:play-services-base'
    api 'com.google.android.gms:play-services-auth'
    api 'com.google.android.material:material'
    api 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    api 'com.google.firebase:firebase-auth'
    api 'com.google.firebase:firebase-messaging'
    api 'it.scoppelletti.spaceship:spaceship'
    api 'it.scoppelletti.spaceship:spaceship-stdlib'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'org.slf4j:slf4j-api'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs'
}
