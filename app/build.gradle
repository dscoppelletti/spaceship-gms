
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    namespace 'it.scoppelletti.spaceship.gms.sample'
    compileSdk sdk_target
    defaultConfig {
        applicationId 'it.scoppelletti.spaceship.gms.sample'
        minSdk sdk_min
        targetSdk sdk_target
        versionCode 1
        versionName '1.0.0'
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

configurations {
    bom
    implementation.extendsFrom(bom)
    kapt.extendsFrom(bom)
    coreLibraryDesugaring.extendsFrom(bom)
}

dependencies {
    bom platform(libs.bom.kotlin)
    bom platform(libs.bom.spaceship)
    implementation project(':lib')
    implementation 'androidx.activity:activity-ktx'
    implementation 'androidx.annotation:annotation'
    implementation 'androidx.appcompat:appcompat'
    implementation 'androidx.lifecycle:lifecycle-common-java8'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx'
    implementation 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'it.scoppelletti.spaceship:spaceship'
    implementation 'it.scoppelletti.spaceship:spaceship-stdlib'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.slf4j:slf4j-api'
    runtimeOnly 'com.noveogroup.android:android-logger'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs'
}
